apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
        incremental false
    }

    lintOptions {
        abortOnError false
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".aar", ".zip"))
        }
    }
}

//rename .aar to .zip and unarchive it.
task updateLib(type: Copy) {
    def aarFile = file("${buildDir}/outputs/aar/mobiledownloadlibrary-debug.zip")
    def outputDir = file("${buildDir}/outputs/aar/mobiledownloadlibrary-debug")
    from zipTree(aarFile)
    into outputDir
}

//copy classes.jar and place the update in correct Unity project folder to use.
task replaceJar(type: Copy) {
    def jarFile = file("${buildDir}/outputs/aar/mobiledownloadlibrary-debug/classes.jar")
    def jarDest = file("../../Assets/Plugins/MobileDownloadPlugin/Android")
    from jarFile
    into jarDest
}

assembleDebug.doLast {
    updateLib.execute()
    replaceJar.execute()
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/unity-classes.jar')
    compile 'com.google.code.gson:gson:2.4'
}
